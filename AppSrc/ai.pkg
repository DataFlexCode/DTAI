Use ai.h
Use cdtJsonHttpTransfer.pkg
Use cMarkdown_mixin.pkg
Use cBase64Attachment_mixin.pkg

// subclass of cdtJsonHttpTransfer gets the cMarkdown_mixin and cBase64Attachment_mixin

Class cAiInterface is a cdtJsonHttpTransfer
    Import_Class_Protocol cMarkdown_mixin
    Import_Class_Protocol cBase64Attachment_mixin
    
    Procedure construct_object
        Forward Send Construct_Object
        
        Send define_cMarkdown_mixin
        Send Define_cBase64Attachment_mixin
        
        Property tAIModel[] paModels 
        Property String psApiKey ''
        Property String psModelResponseJson ''
        Property String psModelRequestJson ''
        Property tRestCallStats pLastCallStats
                
        Set peTransferFlags to ifSecure
        Set piRemotePort to rpHttpSSL
    End_Procedure
    
    Procedure end_construct_object
        Forward Send End_Construct_Object
    End_Procedure
    
    Function CheckApiKeyExists Returns Boolean
        If (psApiKey(Self)='') Begin
            Error DFERR_PROGRAM "Error API Key is not set, add to .env file"
            Function_Return 0
        End
        Else Function_Return 1
    End_Function
    
    Function HttpPostJson String sHost String sFilePath Handle hoJsonRequest Boolean ByRef bOk Returns Handle
        tRestCallStats Stats
        Handle hoResponse
        
        Move (CurrentDateTime()) to Stats.dtStart
        Forward Get HttpPostJson sHost sFilePath hoJsonRequest (&bOk) to hoResponse
        Move (CurrentDateTime()) to Stats.dtStop
        Move (Stats.dtStop-Stats.dtStart) to Stats.tElapsed
        Set pLastCallStats to Stats
        Function_Return hoResponse
    End_Function

    // following functions define the standard inteface for talking to AI models
    // no functionality is provided here, all are intended to be overriden in subclass
    
    // send the request to the AI, return a struct
    Function MakeRequest Handle hoRequest Returns tAIResponse
        Handle hoRetVal
        Error DFERR_PROGRAM ("MakeRequest not implemented"*name(Self)) 
        Function_Return hoRetVal
    End_Function

    // send the request to the AI and return a JSON object containing its response 
    // this should be considered lower level than MakeRequest as it provides access
    //  to the raw JSON response
    Function MakeRequestJSON Handle hoRequest Returns Handle
        Handle hoRetVal
        Error DFERR_PROGRAM ("MakeRequestJSON not implemented"*name(Self)) 
        Function_Return hoRetVal
    End_Function

    // return a list of models available for this AI
    Function ModelList Returns tAIModel[]
        tAIModel[] ModelList        
        Error DFERR_PROGRAM ("ModelList not implemented"*name(Self)) 
        Function_Return ModelList
    End_Function

    // create and return a JSON object containing a request suitable for this AI  
    Function CreateRequest String sPrompt tAIAttachment[] Attachments Returns Handle 
        Handle hoRequest
        Error DFERR_PROGRAM ("CreateRequest not implemented"*name(Self)) 
        Function_Return hoRequest
    End_Function
    
End_Class